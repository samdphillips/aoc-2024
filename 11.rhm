#lang rhombus/static/and_meta

import:
  "util/advent_of_code.rhm" as aoc
  "util/misc.rhm" open
  rhombus/rx open

@example_input(test_input1){
125 17

}

namespace memoize:
  export rename _fun as fun

  class Sentinel()
  def sentinel = Sentinel()

  class Absent()
  def absent = Absent()

  // XXX: handle return annotations
  defn.macro '_fun $(name :: Identifier)($(arg :: bind_meta.Parsed), ...):
                  $body
                  ...':
    def [vars, ...]:
      for List (v: [arg, ...]): Syntax.make_temp_id()
    'def $name:
       block:
         let memo = MutableMap()
         fun inner($arg, ...):
           $body
           ...
         fun($vars, ...):
           def k = Array($vars, ...).snapshot()
           match memo.get(k, absent)
           | _ :: Sentinel:
               error(~who: #'$name, "memoized function has cycle")
           | _ :: Absent:
               memo[k] := sentinel
               // XXX: an error in inner would leave a sentinel
               def val = inner($vars, ...)
               memo[k] := val
               val
           | val: val'

fun read_input(s :: String) :: List.of(NonnegInt):
  // RRR: RX.split should be annot List.of(String)
  for List (ns :~ String: rx'space+'.split(s)):
    skip_when ns == ""
    ns.to_int()

fun count_digits :: NonnegInt
| count_digits(0): 0
| count_digits(n :: NonnegInt):
    1 + count_digits(n div 10)

fun split_number(n :: NonnegInt, i :: NonnegInt):
  def p = math.expt(10, i)
  [n div p, n mod p]

fun is_even(n :: NonnegInt) :: Boolean:
  n rem 2 == 0

fun step(n :: NonnegInt) :~ List.of(NonnegInt):
  cond
  | n == 0: [1]
  | ~else:
      def i = count_digits(n)
      if is_even(i)
      | split_number(n, i / 2)
      | [n * 2024]

memoize.fun stepn(n :~ NonnegInt, i :~ NonnegInt):
  recur loop(ns :~ List = [n], i = i):
    if i == 0
    | ns
    | def [rs, ...]:
        for List (n0: ns):
          each n1: step(n0)
          stepn(n1, i - 1)
      List.append(rs, ...)

fun many([n, ...] :: List.of(NonnegInt), k):
  List.append(stepn(n, k), ...)

fun get_puzzle_input():
  aoc.fetch_input(aoc.find_session(), 2024, 11)

fun run(input, cycles):
  def nums = read_input(input)
  def res :: List = many(nums, cycles)
  res.length()

module test:
  check run(test_input1, 25) ~is 55312

module part1:
  def input = get_puzzle_input()
  run(input, 25)

module part2:
  def input = get_puzzle_input()
  run(input, 75)
